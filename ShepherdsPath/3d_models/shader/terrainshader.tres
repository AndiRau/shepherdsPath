[gd_resource type="Shader" format=2]

[resource]
code = "shader_type spatial;


uniform float noise_influence = 0.2;
uniform float noise_scale = 0.2;
uniform float noise_lacunarity = 2.0;
uniform float noise_gain = 0.5;
uniform int noise_detail = 3;

uniform float rock_noise_influence = 0.2;
uniform float rock_noise_scale = 0.2;
uniform float rock_noise_lacunarity = 2.0;
uniform float rock_noise_gain = 0.5;
uniform int rock_noise_detail = 3;

uniform float rock_coverage = 0.8;
uniform float rock_blend = 0.8;
uniform vec4 color_rocks : hint_color;
uniform sampler2D height_gradient;
uniform mat3 u_terrain_normal_basis;


vec4 permute(vec4 x){return mod(((x*34.0)+1.0)*x, 289.0);}
vec4 taylorInvSqrt(vec4 r){return 1.79284291400159 - 0.85373472095314 * r;}

float snoise(vec3 v){ 
  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

// First corner
  vec3 i  = floor(v + dot(v, C.yyy) );
  vec3 x0 =   v - i + dot(i, C.xxx) ;

// Other corners
  vec3 g = step(x0.yzx, x0.xyz);
  vec3 l = 1.0 - g;
  vec3 i1 = min( g.xyz, l.zxy );
  vec3 i2 = max( g.xyz, l.zxy );

  //  x0 = x0 - 0. + 0.0 * C 
  vec3 x1 = x0 - i1 + 1.0 * C.xxx;
  vec3 x2 = x0 - i2 + 2.0 * C.xxx;
  vec3 x3 = x0 - 1. + 3.0 * C.xxx;

// Permutations
  i = mod(i, 289.0 ); 
  vec4 p = permute( permute( permute( 
             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
           + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

// Gradients
// ( N*N points uniformly over a square, mapped onto an octahedron.)
  float n_ = 1.0/7.0; // N=7
  vec3  ns = n_ * D.wyz - D.xzx;

  vec4 j = p - 49.0 * floor(p * ns.z *ns.z);  //  mod(p,N*N)

  vec4 x_ = floor(j * ns.z);
  vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

  vec4 x = x_ *ns.x + ns.yyyy;
  vec4 y = y_ *ns.x + ns.yyyy;
  vec4 h = 1.0 - abs(x) - abs(y);

  vec4 b0 = vec4( x.xy, y.xy );
  vec4 b1 = vec4( x.zw, y.zw );

  vec4 s0 = floor(b0)*2.0 + 1.0;
  vec4 s1 = floor(b1)*2.0 + 1.0;
  vec4 sh = -step(h, vec4(0.0));

  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

  vec3 p0 = vec3(a0.xy,h.x);
  vec3 p1 = vec3(a0.zw,h.y);
  vec3 p2 = vec3(a1.xy,h.z);
  vec3 p3 = vec3(a1.zw,h.w);

//Normalise gradients
  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
  p0 *= norm.x;
  p1 *= norm.y;
  p2 *= norm.z;
  p3 *= norm.w;

// Mix final noise value
  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
  m = m * m;
  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
                                dot(p2,x2), dot(p3,x3) ) );
}

float fbm(vec3 p, int octaves, float lacunarity, float gain) {
    
    float v = 0.0;
    float amp = 0.5;
    vec3 pp = p;
    
    for (int i = 0; i < octaves; i++) {
        v += amp * snoise(pp);
        pp *= lacunarity;
        amp *= gain;
    }

    return v;
}

float remap(float value, float from1, float to1, float from2, float to2) {
    return (value - from1) / (to1 - from1) * (to2 - from2) + from2;
}

varying vec3 worldnormal;

void vertex() {
	worldnormal = NORMAL;
}

void fragment() {
	
	vec3 vpos = (CAMERA_MATRIX * vec4(VERTEX, 1.0)).xyz;
	float nrock = fbm(vpos * rock_noise_scale, rock_noise_detail, rock_noise_lacunarity, rock_noise_gain);
	float rock_map = 1.0 - (worldnormal.y + nrock * rock_noise_influence - rock_coverage) / rock_blend;
	ALBEDO = mix(vec3(0.9), vec3(0.12), clamp(rock_map, 0.0, 1.0));
}
"
